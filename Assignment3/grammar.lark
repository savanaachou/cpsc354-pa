start: expr

?expr: seq_expr

seq_expr: seq_expr ";;" lam_expr              -> prog
        | lam_expr

?lam_expr: "\\" NAME "." lam_expr            -> lam
         | if_expr

if_expr: "if" lam_expr "then" lam_expr "else" lam_expr   -> ifexp
       | letrec_expr

letrec_expr: "letrec" NAME "=" lam_expr "in" lam_expr -> letrecexp
           | let_expr

let_expr: "let" NAME "=" lam_expr "in" lam_expr      -> letexp
        | fix_expr

fix_expr: "fix" leq_expr                     -> fix
        | leq_expr

?leq_expr: leq_expr "<=" hd_tl_expr          -> leq
         | hd_tl_expr

?hd_tl_expr: "hd" hd_tl_expr                 -> hd
           | "tl" hd_tl_expr                 -> tl
           | eq_expr

?eq_expr: eq_expr "==" cons_expr             -> eq
        | eq_expr "=" cons_expr              -> eq
        | cons_expr

?cons_expr: add_expr ":" cons_expr           -> cons
          | add_expr

?add_expr: add_expr "+" mul_expr             -> plus
         | add_expr "-" mul_expr             -> minus
         | mul_expr

?mul_expr: mul_expr "*" app_expr             -> times
         | app_expr

?app_expr: app_expr neg_atom                 -> app
         | neg_atom

?neg_atom: "-" neg_atom                      -> neg
         | atom

?atom: "(" expr ")"
     | "#"                                   -> nil
     | NUMBER                                -> num
     | NAME                                  -> var

NAME: /[a-zA-Z_][a-zA-Z0-9_]*/
NUMBER: /[0-9]+(\.[0-9]+)?/

%import common.WS
%ignore WS
%ignore /\/\/.*/

// grammar.lark
// Grammar for the calculator assignment.
// Precedence (from highest to lowest):
//   - atoms (NUMBER, parenthesis, log ...)
//   - ^ (right-associative)
//   - unary - (prefix; lower precedence than ^ so that -3^2 == -(3^2))
//   - *
//   - +, - (binary)
%import common.NUMBER
%import common.WS_INLINE
%ignore WS_INLINE

?start: sum

?sum: sum "+" term   -> add
    | sum "-" term   -> sub
    | term

?term: term "*" unary -> mul
     | unary

?unary: "-" unary    -> neg
      | power

?power: atom "^" power  -> pow
      | atom

?atom: "log" atom "base" atom -> log
     | "(" sum ")"
     | NUMBER                   -> number
